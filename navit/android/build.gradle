apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "org.navitproject.navit"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {// 'x86_64' ??
            abiFilters 'x86', 'armeabi-v7a', 'arm64-v8a'
        }
        externalNativeBuild {
            cmake {
                arguments '-DUSE_PLUGINS=n', '-DBUILD_MAPTOOL=n', '-DXSL_PROCESSING=n', '-DSAMPLE_MAP=n'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'UnusedResources'
        abortOnError false
    }
    
    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs = ["src"]
            resources.srcDirs = ["src"]
            renderscript.srcDirs = ["src"]
            res.srcDirs = ["res"]
        }
    }
    externalNativeBuild {
        cmake {
            path '../../CMakeLists.txt'
        }
    }
    
     task checkstyleMain(type: Checkstyle){
        source 'src'
	include '**/*.java'
        configFile = rootProject.file('navit/android/google_checks.xml')
	ignoreFailures = true
        showViolations = true
	// empty classpath
       classpath = files()
        reports {
	     include ( '**/*.java')
	     exclude ('**/NavitBackupTask.java')
	     exclude ('**/NavitRestoreTask.java')
	     exclude ('**/NavitMapDownloader.java')
	     exclude ('**/FileBrowserActivity.java')
	     xml.enabled = true
	     html.enabled = true
	     xml {
	         destination "checkstyle/checkstyleMain.xml"
	     }
	     html {
	         destination "checkstyle/checkstyleMain.html"
	     }
         }
     }
     checkstyle {
         toolVersion = '8.10'
     }
     
    applicationVariants.all { variant ->
        // create tasks to generate Javadocs
        task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
            source = variant.javaCompile.source
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

            // choose the destination that works best for you here
            // I chose this particular directory because Jenkins pulls reports 
            // from this directory already if you need to have the output 
            // folder be parameterized for the build variant, use
            // "build/outputs/docs/javadoc-${variant.name}/" instead and it'll 
            // be in `javadoc-productionRelease` for example
            destinationDir = file("build/outputs/docs/javadoc/")

            // the name that will appear in the docs
            title = rootProject.name
            options.memberLevel = JavadocMemberLevel.PACKAGE
            verbose = true
	    
            // you will probably get errors from using the @annotations and 
            // the support library, so just turn off failing for errors
            failOnError false    
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
//    implementation 'ch.acra:acra:4.9.2'
    implementation 'com.android.support:support-v4:27.1.1'
}
